program     		::= statement {statement}.

statement   		::= formula ";".

formula             ::= conditional_formula | implicit_formula.

conditional_formula ::= "IF" formula "THEN" formula "ELSE" formula.

implicit_formula	::= binary_formula "IMPLIES" implicit_formula
					|   binary_formula "IMPLIES" conditional_formula
					|	binary_formula.

binary_formula		::= binary_formula BINOP not_formula
					|   binary_formula BINOP conditional_formula
					|	not_formula.

not_formula			::= "NOT" not_formula
					|   "NOT" conditional_formula
					|	operand.

operand				::= "TRUE" | "FALSE" | ID | "(" formula ")".

BINOP				::= "AND" | "OR" | "XOR" | "EQUALS". 

ID					::= (letter {letter}) - ("IF" | "THEN" | "ELSE" | "IMPLIES" | "AND" | "OR" | "XOR" | "EQUALS" | "NOT" | "TRUE" | "FALSE").

letter				::= LOWER | UPPER.

LOWER				::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i"
					|	"j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r"
					|	"s" | "t" | "u" | "v" | "w" | "x" | "y" | "z".

UPPER				::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I"
					|	"J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R"
					|	"S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z".					
